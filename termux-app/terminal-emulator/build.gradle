plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id "com.github.dcendents.android-maven" version "2.1"
}

apply plugin: 'com.android.library'

ext {
    bintrayName = 'terminal-emulator'
    publishedGroupId = 'com.termux'
    libraryName = 'TerminalEmulator'
    artifact = 'terminal-emulator'
    libraryDescription = 'The terminal emulator used in Termux'
    siteUrl = 'https://github.com/termux/termux-app'
    gitUrl = 'https://github.com/termux/termux-app.git'
    libraryVersion = '0.52'
}

android {
    compileSdkVersion project.properties.compileSdkVersion.toInteger()
    ndkVersion project.properties.ndkVersion

    defaultConfig {
        minSdkVersion project.properties.minSdkVersion.toInteger()
        targetSdkVersion project.properties.targetSdkVersion.toInteger()

        externalNativeBuild {
            ndkBuild {
                cFlags "-std=c11", "-Wall", "-Wextra", "-Werror", "-Os", "-fno-stack-protector", "-Wl,--gc-sections"
            }
        }

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

tasks.withType(Test) {
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
}

apply from: '../scripts/bintray-publish.gradle'
